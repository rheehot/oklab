apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion build_versions.target_sdk
    buildToolsVersion build_versions.build_tools

    defaultConfig {
        applicationId "com.okitoki.okchat"
        minSdkVersion build_versions.min_sdk
        targetSdkVersion build_versions.target_sdk
        versionCode build_versions.versionCode
        versionName build_versions.versionName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        multiDexEnabled = true
    }
    signingConfigs {
        release {
            storeFile file('keystore/release.keystore')
            keyAlias "$System.env.KEY_ALIAS"
            storePassword "$System.env.STORE_PASSWORD"
            keyPassword "$System.env.KEY_PASSWORD"
        }

        debug {
            storeFile file('keystore/debug.keystore')
            keyAlias 'androiddebugkey'
            storePassword 'android'
            keyPassword 'android'
        }
    }
    buildTypes {
        release {
            signingConfig signingConfigs.release
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), file('proguard-rules.pro')
        }

        debug {
            signingConfig signingConfigs.debug
            versionNameSuffix " Debug"
            testCoverageEnabled true
            debuggable true
        }
        qa {
            minifyEnabled false
            debuggable true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    dataBinding {
        enabled = true
    }

    packagingOptions {
        exclude 'LICENSE.txt'
        exclude 'LICENSE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/rxjava.properties'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    dexOptions {
        preDexLibraries = false
        dexInProcess = false
        javaMaxHeapSize "4g"
    }

    // change default directory path to use kotlin folders
    sourceSets {
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'

        def commonTestDir = 'src/commonTest/kotlin'
        test {
            java.srcDir commonTestDir
        }
        androidTest {
            java.srcDir commonTestDir
        }
    }

//    lintOptions {
//        quiet true
//        abortOnError false
//        ignoreWarnings true
//    }
}

kapt {
    generateStubs = true
    correctErrorTypes = true
}


dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    testImplementation 'junit:junit:4.12'
//    androidTestImplementation 'com.android.support.test:runner:1.0.2'
//    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'

    implementation deps.support.v4
    implementation deps.support.annotations
    implementation deps.support.app_compat
    implementation deps.support.design
    implementation deps.support.recyclerview
    implementation deps.constraint_layout

    // Architecture components
    implementation deps.lifecycle.runtime
    implementation deps.lifecycle.extensions
    implementation deps.room.runtime
    implementation deps.room.rxjava2
    implementation deps.paging_ktx

    // Compiler
    kapt deps.lifecycle.compiler
    kapt deps.room.compiler

    // Kotlin
    implementation deps.kotlin.stdlib

    // RxJava
    implementation deps.rx_android
    implementation deps.rxjava2

    // Koin
    implementation deps.koin.core
    implementation deps.koin.android
    implementation deps.koin.architecture

    // Retrofit
    implementation deps.retrofit.runtime
    implementation deps.retrofit.rx_adapter
    implementation deps.retrofit.gson

    // OkHttp
    implementation deps.okhttp_logging_interceptor
}
